apply plugin: 'com.android.application'
apply plugin: 'robolectric'


android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "org.mythtv.androidtv"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
//            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

robolectric {

    // configure the set of classes for JUnit tests
    include '**/*Tests.class'
    exclude '**/espresso/**/*.class'

    // configure max heap size of the test JVM
    maxHeapSize = '2048m'

    // configure the test JVM arguments
    jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'

    // configure whether failing tests should fail the build
    ignoreFailures true

    // use afterTest to listen to the test execution results
    afterTest { descriptor, result ->
        println "Executing test for {$descriptor.name} with result: ${result.resultType}"
    }

}

repositories {
    mavenCentral()
    mavenLocal()
}

// To use: File->Settings->Compiler->Command-line Options: -PuseAPISnapshot=true
String apiDependency = 'org.mythtv.android:mythtv-service-api:2.0.1'

if (hasProperty('useAPISnapshot')) {
    apiDependency += '-SNAPSHOT'
    println '*** Built using Service API: ' + apiDependency
}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.android.support:recyclerview-v7:21.0.2'
    compile 'com.android.support:leanback-v17:21.0.2'
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile apiDependency

//    compile 'commons-logging:commons-logging:1.1.1'
//    compile 'org.apache.httpcomponents:httpclient:4.0.3'

    androidTestCompile 'org.robolectric:robolectric:2.3'
    androidTestCompile 'junit:junit:4.11'
}
